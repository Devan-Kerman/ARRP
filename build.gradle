plugins {
    id 'fabric-loom' version '1.0-SNAPSHOT'
    id 'maven-publish'
    id "me.shedaniel.unified-publishing" version "0.1.+"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation include(fabricApi.module('fabric-api-base', project.fabric_version))
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}


tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components["java"])
        }
    }
    // select the repositories you want to publish to
    repositories {
        maven {
            url(project.findProperty('maven_url'))
            credentials {
                username(project.findProperty('maven_username'))
                password(project.findProperty('maven_password'))
            }
        }
    }
}

unifiedPublishing {
    project {
        gameVersions = [minecraft_version]
        gameLoaders = ["fabric"]

        mainPublication tasks.remapJar // Declares the publicated jar

        var cfToken = project.findProperty("cf_token")
        if (cfToken != null) {
            curseforge {
                token = cfToken
                id = "463113" // Required, must be a string, ID of CurseForge project
            }
        }

        var mrToken = project.findProperty("mr_token")
        if (mrToken != null) {
            modrinth {
                token = mrToken
                id = "USLVyT7V" // Required, must be a string, ID of Modrinth project
            }
        }
    }
}