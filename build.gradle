plugins {
    id("fabric-loom") version "1.0-SNAPSHOT"
    id("me.shedaniel.unified-publishing") version "0.1.+"
    id("maven-publish")
}

group = "net.devtech"
version = System.getenv("ARRP_VERSION") ?: "0.0.0"

dependencies {
    minecraft("com.mojang:minecraft:${minecraft_version}")
    mappings("net.fabricmc:yarn:${yarn_mappings}:v2")
    modImplementation("net.fabricmc:fabric-loader:${loader_version}")
    modImplementation(include(fabricApi.module("fabric-api-base", fabric_version)))
}

base {
    archivesName.set("arrp")
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }

    withSourcesJar()
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

processResources {
    inputs.property("version", project.version)

    filesMatching("fabric.mod.json") {
        expand("version": project.version)
    }
}

jar {
    from("LICENSE") {
        rename { "${it}_${base.archivesName.get()}"}
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
        }
    }

    repositories {
        maven {
            name = "Project"
            url = uri("${buildDir}/repo")
        }

        maven {
            credentials {
                username = ""
                password = System.getenv("MODMAVEN")
            }

            name = "Modmaven"
            url = "https://modmaven.dev/artifactory/local-releases/"
        }
    }
}

unifiedPublishing {
    project {
        gameVersions.add(minecraft_version)
        gameVersions.add("fabric")

        changelog.set("View changelog at [the release page](https://github.com/Devan-Kerman/ARRP/releases/tag/${version})")

        mainPublication(tasks.remapJar)

        System.getenv("CURSEFORGE")?.with { String key ->
            curseforge {
                token.set(key)
                id.set("463113")
            }
        }

        System.getenv("MODRINTH")?.with { String key ->
            modrinth {
                token.set(key)
                id.set("USLVyT7V")
            }
        }
    }
}
